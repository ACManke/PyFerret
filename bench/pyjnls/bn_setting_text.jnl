! bn_setting_text.jnl
! 4/10/2019 ACM 

! SET TEXT with customizations for text groupings
! tests of syntax-checking, see ticket 1923
!
! 4/30/19 add tests of text location and centering, issue 1927

set text/font=helvetica

set view ul
define axis/x=0:20:1/units=degrees_east xaxis
define axis/y=0:20:1/units=degrees_north yaxis
let xx = x[gx=xaxis]
let yy = y[gy=yaxis]

contour/color=(20,70,70)/thick/title="contour"  10*sin(xx/3)*cos(yy/4)


set text/color=red contour
set text/isiz=3/bold title
set text/font=serif/bold/color=blue hlab
set text/font=serif/bold/color=purple vlab

set view ur
contour/color=(20,70,70)/thick/title="With SET TEXT customizing text groups"  10*sin(xx/3)*cos(yy/4)

cancel text

! Tests for text centering and location when there are 
! subscripts, superscriptsand special characters

set view ll
set text/font=arial
let/title="Xpts title with many deltas: <&Delta> <&Delta> <&Delta> <&Delta> <&Delta> <&Delta>" xpts = {0,1}
let/title="Ypts title with degree sign <&deg> C" ypts = {0,1}
plot/x=0:3/y=0:3/vs/title= "K<sup>2</sup> / (here are the denominator units)" xpts, ypts
set text/font=courier/bold
annotate/norm/xpos=0.5/ypos=0.5/halign=1/siz=0.12 "Left with<sub>3</sub> sub"
annotate/norm/xpos=0.5/ypos=0.4/halign=-1/siz=0.12 "Right with<sup>2</sup> super"
annotate/norm/xpos=0.5/ypos=0.6/halign=0/siz=0.12 "Centered with sub<sub>2</sub> and super <sup>0.5</sup>"
cancel text/bold

! More tests for html symbols - specifying by name or number, multiple symbols in a single label.
set view lr
set text/font=arial
let/title="Xpts" xpts = {0,1}
let/title="Ypts" ypts = {0,1}
plot/x=0:3/y=0:3/vs/title= " " xpts, ypts
annotate/norm/xpos=0.5/ypos=0.5/halign=0/siz=0.12 "numeric html symbols: <#229> <#248> <#8712> <#8731> <#8734> <#8751>"

! 27-apr-2024   Why did this use &# ? It should be just #
!annotate/norm/xpos=0.5/ypos=0.4/halign=0/siz=0.12 "numeric html symbols, specifying w/ &#   <&#8704> <&#8721>"
annotate/norm/xpos=0.5/ypos=0.4/halign=0/siz=0.12 "numeric html symbols, specifying w/ #   <#8704> <#8721>"

frame/file=set_text.png

! Intentional errors on SET TEXT

set mode ignore
cancel text

! no font-name given
set text/font

! Customizations not valid for Hershey font
set text/font=hershey/color=green/bold

! If a general SET TEXT wasn't issued, text grouping settings are invalid
set text/isiz=3/bold title

! isize valid in the range -4:4
set text/font=helvetica/isiz=6

! Unknown color name
set text/color=indigo/font=helvetica

! Invalid text-group name
set text/font=helvetica 
set text/bold  nogroup

cancel text
set mode/last ignore 
